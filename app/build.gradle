import groovy.xml.XmlSlurper

plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}

android {
    namespace 'com.tencent.iot.twcall'
    compileSdk 34

    defaultConfig {
        applicationId "com.tencent.iot.twcall"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
    buildFeatures {
        viewBinding true
    }
    applicationVariants.configureEach { variant ->
        variant.outputs.configureEach { output ->
            def newApkName = "${getAppName()}-${variant.buildType.name}-${variant.versionName}.apk"
            outputFileName = newApkName
        }
    }
}

def getAppName() {
    def stringsFile = file("${projectDir}/src/main/res/values/strings.xml")
    def strings = new XmlSlurper().parse(stringsFile)
    def appName = strings.string.find { it.@name == 'app_name' }.text()
    return appName
}

dependencies {
    implementation libs.androidx.appcompat
    implementation libs.material
    implementation libs.androidx.constraintlayout
    implementation libs.crashreport
    implementation libs.androidx.core.ktx
    implementation libs.kotlinx.coroutines.android
    implementation 'com.tencent.iot.video:video-device-android:1.0.3-SNAPSHOT'
}